# -*- mode: Snakemake -*-
#
# Rules for running Kraken

rule _kraken_all:
    input:
        expand(str(CLASSIFY_FP/'kraken'/'{sample}-{type}.tsv'),
        sample=Samples.keys(), type=['taxa','mpa']),
        expand(str(CLASSIFY_FP/'kraken'/'krona'/'{sample}-krona.html'),
               sample=Samples.keys())
        
rule kraken_biom:
    input:
        expand(str(CLASSIFY_FP/'kraken'/'{sample}-taxa.tsv'),
               sample=Samples.keys())
    output:
        str(CLASSIFY_FP/'kraken'/'all_samples.biom')
    shell:
        """
        kraken-biom --max D -o {output} {input}
        """

rule classic_biom:
    input:
        str(CLASSIFY_FP/'kraken'/'all_samples.biom')
    output:
        str(CLASSIFY_FP/'kraken'/'all_samples.tsv')
    shell:
        """
        biom convert -i {input} -o {output} \
        --to-tsv --header-key=taxonomy --process-obs-metadata=taxonomy \
        --output-metadata-id="Consensus Lineage"
        """
        
rule kraken_classify:
    input:
        r1=str(QC_FP/'decontam'/'{sample}_R1.fastq'),
        r2=str(QC_FP/'decontam'/'{sample}_R2.fastq')
    output:
        str(CLASSIFY_FP/'kraken'/'raw'/'{sample}-raw.tsv')
    threads:
        Cfg['classify']['threads']
    shell:
        """
        kraken \
        --db {Cfg[classify][kraken_db_fp]} \
        --threads {threads} \
        --paired \
        --fastq-input \
        {input.r1} {input.r2} > {output}
        """

rule kraken_report:
    input:
        str(CLASSIFY_FP/'kraken'/'raw'/'{sample}-raw.tsv')
    output:
        str(CLASSIFY_FP/'kraken'/'{sample}-taxa.tsv')
    shell:
        """
        kraken-report \
        --db {Cfg[classify][kraken_db_fp]} \
        {input} > {output}
        """

rule kraken_mpa_report:
    input:
        str(CLASSIFY_FP/'kraken'/'raw'/'{sample}-raw.tsv')
    output:
        str(CLASSIFY_FP/'kraken'/'{sample}-mpa.tsv')
    shell:
        """
        kraken-mpa-report \
        --db {Cfg[classify][kraken_db_fp]} \
        {input} > {output}
        """
        
rule kraken_krona:
    input:
        str(CLASSIFY_FP/'kraken'/'{sample}-taxa.tsv'),
        str(Cfg['classify']['taxa_db_fp']/'taxonomy.tab')		
    output:
        str(CLASSIFY_FP/'kraken'/'krona'/'{sample}-krona.html')
    shell:
        """
        ktImportTaxonomy \
        -o {output} \
        -t 5 \
        -m 3 \
        -tax {Cfg[classify][taxa_db_fp]} \
        {input}
        """
